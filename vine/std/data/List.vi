
use data::Iterator;
use ops::{Cast, Concat, comparison::{Eq, Ord}, range::{Bound, Range}};
use debug::Show;

#[builtin = "List"]
pub struct List[T](pub.std (N32, Buf[T], ~Buf[T]));

pub.std struct Buf[T](pub.std (T, Buf[T]));

pub mod List {
  pub fn .len[T](&self: &List[T]) -> N32 {
    self.0
  }

  pub fn new[T](len: N32, value: T) -> List[T] {
    let list = [];
    while len > 0 {
      list.push_back(value);
      len -= 1;
    }
    list
  }

  pub fn .get[T](&List[T](len, *buf, _), i: N32) -> &T {
    while i != 0 {
      let &Buf(_, *tail) = buf;
      buf = tail;
      i -= 1;
    }
    let &Buf(*head, _) = buf;
    head
  }

  pub fn .slice[T, B1, B2; Bound[B1, N32], Bound[B2, N32]](self: List[T], Range[B1, B2](start, end)) -> List[
    T,
  ] {
    // Underflow does not matter, still correct
    let end_index = self.len() - 1;
    let i = 0;
    let slice = [];

    while !start.left_of(&i) && self.pop_front() is Some(_) {
      i += 1;
    }
    if end.right_of(&end_index) {
      return self;
    }
    while end.right_of(&i) && self.pop_front() is Some(value) {
      slice.push_back(value);
      i += 1;
    }
    slice
  }

  pub fn .split_at[T](self: List[T], i: N32) -> (List[T], List[T]) {
    if i >= self.len() {
      return (self, []);
    }
    let prefix = [];
    while i != 0 && self.pop_front() is Some(value) {
      prefix.push_back(value);
      i -= 1;
    }
    (prefix, self)
  }

  pub fn .map[T, U](List[T](l, buf, ~_), f: fn(T) -> U) -> List[U] {
    let len = l;
    let cur;
    let result = move ~cur;
    while l != 0 {
      let Buf(head, tail) = buf;
      buf = tail;
      let next;
      ~cur = Buf(f(head), move ~next);
      ~next = move ~cur;
      l -= 1;
    }
    List(len, result, move cur)
  }

  pub fn .pop_front[T](&List[T](len, buf, _)) -> Option[T] {
    if len != 0 {
      len -= 1;
      let Buf(head, tail) = buf;
      buf = tail;
      Some(head)
    } else {
      None
    }
  }

  pub impl concat[T]: Concat[List[T], List[T], List[T]] {
    fn .concat(a: List[T], b: List[T]) -> List[T] {
      let List(a_len, a_buf, ~a_end) = a;
      let List(b_len, b_buf, ~b_end) = b;
      a_end = b_buf;
      List(a_len + b_len, a_buf, move ~b_end)
    }
  }

  pub fn .push_back[T](&list: &List[T], el: T) {
    list ++= [el];
  }

  pub fn .push_front[T](&list: &List[T], el: T) {
    list = [el] ++ list;
  }

  pub fn .insert[T](&List[T](len, *buf, _), i: N32, el: T) {
    len += 1;
    while i != 0 {
      let &Buf(_, *tail) = buf;
      buf = tail;
      i -= 1;
    }
    let &buf = buf;
    buf = Buf(el, buf);
  }

  pub fn .join[T; Cast[T, String]](list: List[T], sep: String) -> String {
    let it = list.into_iter();
    if it.next() is Some(val) {
      let str = "{val}";
      while it.next() is Some(val) {
        str ++= "{sep}{val}";
      }
      str
    } else {
      ""
    }
  }

  pub fn .drop_while[T](&List[T](len, buf, ~_), pred: fn(&T) -> Bool) {
    while len != 0 {
      let Buf(head, tail) = buf;
      if !pred(&head) {
        // Assign buffer again to prevent cloning in the else case
        buf = Buf(head, tail);
        break;
      }
      buf = tail;
      len -= 1;
    }
  }

  pub impl show[T; Show[T]]: Show[List[T]] {
    fn .show(&self: &List[T]) -> Show {
      let entries = [];
      let iter = self.iter();
      while iter.next() is Some(&value) {
        entries.push_back(value.show());
      }
      Show::List(entries)
    }
  }

  pub fn .sort_by[T](&self: &List[T], cmp: fn(&T, &T) -> Bool) {
    let output = [];
    while self.pop_front() is Some(a) {
      let left = [];
      while output.pop_front() is Some(b) {
        if cmp(&a, &b) {
          output.push_front(b);
          break;
        } else {
          left.push_back(b);
        }
      }
      output = left ++ [a] ++ output;
    }
    self = output;
  }

  pub struct Iter[T](pub.std (N32, &Buf[T]));

  pub fn .iter[T](&List[T](len, buf, _)) -> Iter[T] {
    Iter(len, &buf)
  }

  pub mod Iter {
    pub impl iterator[T]: Iterator[Iter[T], &T] {
      fn .next(&Iter[T](len, buf)) -> Option[&T] {
        if len != 0 {
          len -= 1;
          let &Buf(*head, *tail) = buf;
          buf = tail;
          Some(head)
        } else {
          let &_ = move buf;
          None
        }
      }

      fn .drop_iter(&iter: &Iter[T]) {
        let Iter(_, &_) = move iter;
      }
    }
  }

  pub struct IntoIter[T](pub.std (N32, Buf[T]));

  pub fn .into_iter[T](List[T](len, buf, _)) -> IntoIter[T] {
    IntoIter(len, buf)
  }

  pub mod IntoIter {
    pub impl iterator[T]: Iterator[IntoIter[T], T] {
      fn .next(&IntoIter[T](len, buf)) -> Option[T] {
        if len != 0 {
          len -= 1;
          let Buf(head, tail) = buf;
          buf = tail;
          Some(head)
        } else {
          None
        }
      }

      fn .drop_iter(iter: &IntoIter[T]) {}
    }
  }

  pub fn .reversed[T](self: List[T]) -> List[T] {
    let reversed = [];
    while self.pop_front() is Some(value) {
      reversed.push_front(value);
    }
    reversed
  }

  pub fn .reverse[T](&self: &List[T]) {
    self = self.reversed();
  }

  pub impl eq[T; Eq[T]]: Eq[List[T]] {
    fn eq(&a: &List[T], &b: &List[T]) -> Bool {
      if a.len() != b.len() {
        return false;
      }
      let a_iter = a.iter();
      let b_iter = b.iter();
      while a_iter.next() is Some(&a_elem) && b_iter.next() is Some(&b_elem) {
        if a_elem != b_elem {
          a_iter.drop_iter();
          b_iter.drop_iter();
          return false;
        }
      }
      true
    }

    fn ne(&a: &List[T], &b: &List[T]) -> Bool {
      !(a == b)
    }
  }

  pub impl ord[T; Ord[T]]: Ord[List[T]] {
    fn cmp(&a: &List[T], &b: &List[T]) -> Ord {
      let a = a.iter();
      let b = b.iter();
      let ord = loop {
        match (a.next(), b.next()) {
          (None, None) { break Ord::Eq }
          (None, Some(&_)) { break Ord::Lt }
          (Some(&_), None) { break Ord::Gt }
          (Some(&a), Some(&b)) {
            match a.cmp(&b) {
              Ord::Lt { break Ord::Lt }
              Ord::Eq {}
              Ord::Gt { break Ord::Gt }
            }
          }
        }
      };
      a.drop_iter();
      b.drop_iter();
      ord
    }

    fn lt(a: &List[T], b: &List[T]) -> Bool {
      Ord::lt_from_cmp[List[T]](a, b)
    }

    fn le(a: &List[T], b: &List[T]) -> Bool {
      Ord::le_from_cmp[List[T]](a, b)
    }
  }

  pub impl fork[T+]: Fork[List[T]] {
    fn .fork(&self: &List[T]) -> List[T] {
      let iter = self.iter();
      let out = [];
      while iter.next() is Some(&value) {
        out.push_back(value.fork());
      }
      out
    }
  }

  pub impl drop[T?]: Drop[List[T]] {
    fn .drop(self: List[T]) {
      let iter = self.into_iter();
      while iter.next() is Some(value) {
        value.drop();
      }
    }
  }
}
