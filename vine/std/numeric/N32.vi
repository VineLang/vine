
use data::Map::Ord;

#[builtin = "N32"]
pub mod N32 {
  pub const maximum: N32 = 0xffffffff;

  pub fn to_string(n: N32) -> String {
    if n != 0 {
      let chars = [];
      while n != 0 {
        chars = ['0' + n % 10] ++ chars;
        n = n / 10;
      }
      String({ chars })
    } else {
      "0"
    }
  }

  pub fn parse(str: String) -> Option[N32] {
    let String({ chars: List(len, chars, _) }) = str;
    if len == 0 {
      None
    } else {
      let num = 0;
      while len != 0 {
        let List::Buf(char, tail) = chars;
        chars = tail;
        if !('0' <= char <= '9') {
          return None;
        }
        num *= 10;
        num += char - '0';
        len -= 1;
      }
      Some(num)
    }
  }

  pub inline_ivy! rotate_left: fn(N32, N32) -> N32 {
    fn(@n32_rotl(x y) fn(x y))
  }

  pub inline_ivy! rotate_right: fn(N32, N32) -> N32 {
    fn(@n32_rotr(x y) fn(x y))
  }

  pub fn ascending(&a: &N32, &b: &N32) -> Bool {
    a < b
  }

  pub fn descending(&a: &N32, &b: &N32) -> Bool {
    b < a
  }

  pub fn cmp(&a: &N32, &b: &N32) -> Ord {
    if a < b {
      Ord::Lt
    } else if a > b {
      Ord::Gt
    } else {
      Ord::Eq
    }
  }

  pub fn min(a: N32, b: N32) -> N32 {
    if a < b {
      a
    } else {
      b
    }
  }

  pub fn max(a: N32, b: N32) -> N32 {
    if a > b {
      a
    } else {
      b
    }
  }

  pub fn diff(a: N32, b: N32) -> N32 {
    if a > b {
      a - b
    } else {
      b - a
    }
  }

  pub fn gcd(a: N32, b: N32) -> N32 {
    while a != 0 {
      (a, b) = (b % a, a)
    }
    b
  }

  pub fn sqrt(n: N32) -> N32 {
    let r = 0x8000;
    let s = 0;
    while r != 0 {
      let m = s + r;
      if m * m <= n {
        s = m;
      }
      r >>= 1;
    }
    s
  }

  pub fn pow(b: N32, e: N32) -> N32 {
    let n = 1;
    while e != 0 {
      if e % 2 != 0 {
        n *= b;
      }
      b *= b;
      e /= 2;
    }
    n
  }

  pub fn log(n: N32, b: N32) -> N32 {
    let ~s = 0;
    let r = ~s;
    let e = 1;
    let ~m = n;
    while b < n {
      let &m = &~m;
      if b <= m {
        ~s += e;
        m /= b;
      }
      if b >= 65536 {
        break;
      }
      b *= b;
      e *= 2;
    }
    r
  }
}
