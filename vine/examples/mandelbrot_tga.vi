
use std::{io::{print_byte, print_bytes}, hint::black_box};

const max_iter = black_box(64);
const width = black_box(1024);
const height = black_box(1024);
const scale = 2.6;
const center = (-0.8, 0.0);

fn main(io) {
  io.print_bytes([
    0, 0, 3, 0, 0, 0, 0, 0, 0, 0, 0, 0,
    width, width >> 8,
    height, height >> 8,
    8, 32,
  ]);
  let j = 0;
  while j < height {
    let i = 0;
    while i < width {
      let (x, y) = center + scale / width * ((i, j) + 0.5 - (width, height) / 2.0);
      io.print_byte(mandelbrot(x, y));
      i += 1;
    }
    j += 1;
  }
}

fn mandelbrot(x0, y0) {
  let (x, y, x2, y2) = 0.0;
  let i = 0;
  while (i < max_iter) & (x2 + y2 < 4.0) {
    i += 1;
    (x, y) = (x2 - y2 + x0, 2.0 * x * y + y0);
    x2 = x * x;
    y2 = y * y;
  };
  i * 256 / max_iter
}
