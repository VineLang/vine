
const max_iter: u32 = 64;
const width: u32 = 1024;
const height: u32 = 1024;
const scale: f32 = 2.6;
const center: (f32, f32) = (-0.8, 0.0);

pub fn main(&io: &IO) {
  io.print_bytes([0, 0, 3, 0, 0, 0, 0, 0, 0, 0, 0, 0, width, width >> 8, height, height >> 8, 8, 32]);
  let j = 0;
  while j < height {
    let i = 0;
    while i < width {
      let (x, y) = center + scale / width * ((i, j) + 0.5 - (width, height) / 2.0);
      io.print_byte(mandelbrot(x, y));
      i += 1;
    }
    j += 1;
  }
}

fn mandelbrot(x0: f32, y0: f32) -> u32 {
  let x = 0.0;
  let y = 0.0;
  let x2 = 0.0;
  let y2 = 0.0;
  let i = 0;
  while (i < max_iter) & (x2 + y2 < 4.0) {
    i += 1;
    (x, y) = (x2 - y2 + x0, 2.0 * x * y + y0);
    x2 = x * x;
    y2 = y * y;
  };
  i * 256 / max_iter
}
