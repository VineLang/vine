
use std::{io::print_char, hint::black_box};

const max_iter = black_box(128);
const width = black_box(125);
const height = black_box(45);
const scale = 2.6;
const center = (-0.8, 0.0);

fn main(io) {
  let j = 0;
  while j < height {
    let i = 0;
    while i < width {
      let (x, y) = center + scale / width * ((i, j) + 0.5 - (width, height) / 2.0) * (1.0, 2.0);
      io.print_char(if mandelbrot(x, y) { '#' } else { ' ' });
      i += 1;
    }
    io.print_char('\n');
    j += 1;
  }
}

fn mandelbrot(x0, y0) {
  let (x, y, x2, y2) = 0.0;
  let i = max_iter;
  while (i > 0) & (x2 + y2 < 4.0) {
    i -= 1;
    (x, y) = (x2 - y2 + x0, 2.0 * x * y + y0);
    x2 = x * x;
    y2 = y * y;
  }
  i == 0
}
