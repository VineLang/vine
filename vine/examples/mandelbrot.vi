
use #root::ops::elementwise;

const max_iter: N32 = 128;
const width: N32 = black_box(125);
const height: N32 = black_box(45);
const scale: F32 = 2.6;
const center: (F32, F32) = (-0.8, 0.0);

pub fn main(&io: &IO) {
  for j in 0..height {
    for i in 0..width {
      let pos = (i as F32 + 0.5, j as F32 + 0.5) - (width as F32, height as F32) / 2.0;
      let (x, y) = center + pos * scale / width as F32 * (1.0, 2.0);
      io.print_char(if mandelbrot(x, y) {
        '#'
      } else {
        ' '
      });
    }
    io.print_char('\n');
  }
}

fn mandelbrot(x0: F32, y0: F32) -> Bool {
  let x = 0.0;
  let y = 0.0;
  let x2 = 0.0;
  let y2 = 0.0;
  let i = max_iter;
  while (i > 0) & (x2 + y2 < 4.0) {
    i -= 1;
    (x, y) = (x2 - y2 + x0, 2.0 * x * y + y0);
    x2 = x * x;
    y2 = y * y;
  }
  i == 0
}
