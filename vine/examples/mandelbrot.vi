
const max_iter: N32 = 128;
const width: N32 = 125;
const height: N32 = 45;
const scale: F32 = 2.6;
const center: (F32, F32) = (-0.8, 0.0);

pub fn main(&io: &IO) {
  let j = 0;
  while j < height {
    let i = 0;
    while i < width {
      let (x, y) = center + scale / width * ((i, j) + 0.5 - (width, height) / 2.0) * (1.0, 2.0);
      io.print_char(if mandelbrot(x, y) {
        '#'
      } else {
        ' '
      });
      i += 1;
    }
    io.print_char('\n');
    j += 1;
  }
}

fn mandelbrot(x0: F32, y0: F32) -> Bool {
  let x = 0.0;
  let y = 0.0;
  let x2 = 0.0;
  let y2 = 0.0;
  let i = max_iter;
  while (i > 0) & (x2 + y2 < 4.0) {
    i -= 1;
    (x, y) = (x2 - y2 + x0, 2.0 * x * y + y0);
    x2 = x * x;
    y2 = y * y;
  }
  i == 0
}
