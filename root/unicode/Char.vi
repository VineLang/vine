
use ops::{Cast, arithmetic::{Add, Sub}, comparison::{Eq, Ord}};

/// A Unicode scalar value, or "character".
/// ```vi
/// "Vine" as List[Char] // ['V', 'i', 'n', 'e']
/// ['V', 'i', 'n', 'e'] as String // "Vine"
/// ```
#[builtin = "Char"]
pub type Char;

pub mod Char {
  pub impl : Fork[Char] = unsafe::duplicate;
  pub impl : Drop[Char] = unsafe::erase;

  pub impl from_n32: Cast[N32, Char] = unsafe::transmute;
  pub impl to_n32: Cast[Char, N32] = unsafe::transmute;

  pub impl : Eq[Char] {
    fn eq(&a: &Char, &b: &Char) -> Bool {
      inline_ivy! (a <- a, b <- b) -> Bool { out a = @n32_eq(b out) }
    }

    fn ne(&a: &Char, &b: &Char) -> Bool {
      inline_ivy! (a <- a, b <- b) -> Bool { out a = @n32_ne(b out) }
    }
  }

  pub impl : Ord[Char] {
    fn lt(&a: &Char, &b: &Char) -> Bool {
      inline_ivy! (a <- a, b <- b) -> Bool { out a = @n32_lt(b out) }
    }

    fn le(&a: &Char, &b: &Char) -> Bool {
      inline_ivy! (a <- a, b <- b) -> Bool { out a = @n32_le(b out) }
    }

    fn cmp(a: &Char, b: &Char) -> Ord {
      Ord::cmp_from_lt(a, b)
    }
  }

  pub fn .is_whitespace(char: Char) -> Bool {
    (char == ' ') | (char == '\t') | (char == '\n')
  }

  pub fn .is_alphanumeric(char: Char) -> Bool {
    ('a' <= char <= 'z') | ('A' <= char <= 'Z') | ('0' <= char <= '9')
  }

  pub fn .escape(char: Char) -> String {
    when {
      ' ' <= char <= '~' { "{char}" }
      char == '\0' { "\\0" }
      char == '\t' { "\\t" }
      char == '\r' { "\\r" }
      char == '\n' { "\\n" }
      char as N32 < 16 { "\\x0{char.as[N32].to_hex_raw()}" }
      char as N32 < 128 { "\\x{char.as[N32].to_hex_raw()}" }
      _ { "\\u\{{char.as[N32].to_hex_raw()}}" }
    }
  }

  pub impl to_string: Cast[Char, String] {
    fn cast(self: Char) -> String {
      [self] as String
    }
  }

  pub impl : Show[Char] {
    fn show(&char: &Char) -> Show {
      Show::Literal("'{
        when {
          char == '\'' { "\\'" }
          _ { char.escape() }
        }
      }'")
    }
  }

  pub impl add_n32: Add[Char, N32, Char] {
    fn add(a: Char, b: N32) -> Char {
      inline_ivy! (a <- a, b <- b) -> Char { out a = @n32_add(b out) }
    }
  }

  pub impl sub_n32: Sub[Char, N32, Char] {
    fn sub(a: Char, b: N32) -> Char {
      inline_ivy! (a <- a, b <- b) -> Char { out a = @n32_sub(b out) }
    }
  }

  pub impl sub_char: Sub[Char, Char, N32] {
    fn sub(a: Char, b: Char) -> N32 {
      inline_ivy! (a <- a, b <- b) -> N32 { out a = @n32_sub(b out) }
    }
  }
}
