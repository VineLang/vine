
use #root::data::Iterator::Fused;

pub fn main(&io: &IO) {
  let lines = [];
  while io.read_line() is Some(String(chars)) {
    lines ++= [chars];
  }

  let matches = 0;
  diamond(
    lines,
    fn* (char: Char, Neighbors[Char](nw, n, ne, w, e, sw, s, se)) {
      when {
        char == 'X' {
          nw.send('X');
          n.send('X');
          ne.send('X');
          w.send('X');
          e.send('X');
          sw.send('X');
          s.send('X');
          se.send('X');
        }
        char == 'M' or char == 'A' {
          let recv = if char == 'M' {
            'X'
          } else {
            'M'
          };
          relay(nw, se, recv, char);
          relay(n, s, recv, char);
          relay(ne, sw, recv, char);
          relay(w, e, recv, char);
        }
        _ {
          matches += check(nw, 'A') + check(ne, 'A') + check(sw, 'A') + check(se, 'A');
          let x = check(n, 'A') + check(s, 'A') + check(w, 'A') + check(e, 'A');
          matches += x;
        }
      }
    },
    '.',
  );

  io.println("XMAS: {matches}");

  let matches = 0;
  diamond(
    lines,
    fn* (char: Char, Neighbors[Char](nw, _n, ne, _w, _e, sw, _s, se)) {
      safe erase((_n, _w, _e, _s));
      if char == 'A' {
        let nw = nw.send('A');
        let ne = ne.send('A');
        let sw = sw.send('A');
        let se = se.send('A');
        if ((nw == 'M' and se == 'S') or (nw == 'S' and se == 'M')) and ((sw == 'M' and ne == 'S') or (
          sw == 'S' and ne == 'M'
        )) {
          matches += 1;
        }
      } else {
        nw.send(char);
        ne.send(char);
        sw.send(char);
        se.send(char);
      }
    },
    '.',
  );

  io.println("X-MAS: {matches}");
}

struct Channel[M]((M, ~M));

mod Channel {
  pub fn .send[M](Channel[M](got, ~out), value: M) -> M {
    out = value;
    got
  }
}

fn relay(Channel[Char](ai, ~ao), Channel[Char](bi, ~bo), recv: Char, send: Char) {
  bo = if ai == recv {
    send
  } else {
    '_'
  };
  ao = if bi == recv {
    send
  } else {
    '_'
  };
}

fn check(Channel[Char](i, ~o), recv: Char) -> N32 {
  o = '_';
  if i == recv {
    1
  } else {
    0
  }
}

struct Neighbors[M]((
  Channel[M],
  Channel[M],
  Channel[M],
  Channel[M],
  Channel[M],
  Channel[M],
  Channel[M],
  Channel[M],
));

fn diamond[T*, M*, F*; fn F(T, Neighbors[M])](grid: List[List[T]], f: F, d: M) {
  let width = (*grid.at(0).assume()).len();
  let north = List::from_fn(width, fn* () { neglect_channel(d) });
  let north_west = List::from_fn(width, fn* () { neglect_channel(d) });
  let north_east = List::from_fn(width, fn* () { neglect_channel(d) });
  for row in grid.iter() {
    let west = neglect_channel(d);
    do {
      let north = Fused::new(north.iter_ref());
      let north_west = Fused::new(north_west.iter_ref());
      let ~north_east = Fused::new(north_east.iter_ref());
      for cell in row.iter() {
        let (w, e) = foo_channel(&west);
        let (n, s) = foo_channel(north.next().assume());
        let (nw, se) = foo_channel(north_west.next().assume());
        let (ne, sw) = foo_channel((~north_east).next().assume());
        f(cell, Neighbors(nw, n, ne, w, e, sw, s, se));
      }
    }
    west.send(d);
    north_west.pop_front().assume().send(d);
    north_west ++= [neglect_channel(d)];
    north_east.pop_front().assume().send(d);
    north_east ++= [neglect_channel(d)];
  }
  drop_all(north, d);
  drop_all(north_west, d);
  drop_all(north_east, d);
}

fn drop_all[M*](c: List[Channel[M]], d: M) {
  for c in c.iter() {
    c.send(d);
  }
}

fn foo_channel[M](&c: &Channel[M]) -> (Channel[M], Channel[M]) {
  let x = c;
  let (a, b) = new_channel();
  c = a;
  (x, b)
}

fn new_channel[M]() -> (Channel[M], Channel[M]) {
  let a;
  let b;
  let x = ~a;
  let y = ~b;
  (Channel(a, y), Channel(b, x))
}

fn neglect_channel[M](d: M) -> Channel[M] {
  Channel(d, safe eraser)
}
