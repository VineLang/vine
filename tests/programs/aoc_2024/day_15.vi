
use #root::{data::Array, ops::elementwise};

pub fn main(&io: &IO) {
  let grid = Array::empty;
  let y = 0;
  let initial = (0, 0);
  while io.read_line() is Some(line) && line.len() != 0 {
    let x = 0;
    for &char in line.iter() {
      if char == '@' {
        initial = (x, y);
        char = '.';
        break;
      };
      x += 1;
    }
    grid.push_back(line as List as Array);
    y += 1;
  }

  let initial_grid = grid;

  let fn get((x, y)) {
    (*grid.at(y).unwrap()).at(x).unwrap()
  }

  let fn show_grid() {
    io.println(grid.as[List].map(fn* (array: Array) { array as List as String }).join("\n"));
  }

  let instructions = [];
  let robot = initial;
  while io.read_line() is Some(line) {
    for char in line.into_iter() {
      instructions ++= [char];
      let dir = when {
        char == '<' { (0 - 1, 0) }
        char == '>' { (1, 0) }
        char == '^' { (0, 0 - 1) }
        _ { (0, 1) }
      };
      let target_pos = robot + dir;
      while *get(target_pos) == 'O' {
        target_pos += dir;
      }
      let &target = get(target_pos);
      if target == '.' {
        target = 'O';
        robot += dir;
        *get(robot) = '.';
      }
    }
  }

  let part1 = 0;
  let y = 0;
  for row in grid.as[List].into_iter() {
    let x = 0;
    for char in row.as[List].into_iter() {
      if char == 'O' {
        part1 += y * 100 + x;
      }
      x += 1;
    }
    y += 1;
  }

  show_grid();

  io.println("Part 1: {part1}");

  grid = initial_grid.as[List].map(fn* (row: Array[Char]) {
    let new_row = [];
    for char in row.as[List].into_iter() {
      if char == 'O' {
        new_row ++= "[]" as List;
      } else {
        new_row ++= [char, char];
      }
    }
    new_row as Array
  }).as[Array];

  let robot = initial * (2, 1);

  for char in instructions.into_iter() {
    if char == '<' || char == '>' {
      let dir = if char == '<' {
        (0 - 1, 0)
      } else {
        (1, 0)
      };
      let ~success;
      let pos = robot;
      if ~success {
        robot += dir;
      }
      let carry = '.';
      loop {
        pos += dir;
        let &char = get(pos);
        if char == '#' {
          ~success = false;
          break;
        }
        if char == '.' {
          char = carry;
          ~success = true;
          break;
        }
        let old = char;
        if ~success {
          char = carry;
        }
        carry = old;
        continue;
      }
    } else {
      let dir_y = if char == '^' {
        -1 as N32
      } else {
        1
      };
      let (robot_x, robot_y) = robot;
      let front = [(robot_x, '.')];
      let pos_y = robot_y;
      let ~success = true;
      if ~success {
        robot += (0, dir_y);
      }
      while.outer front.len() != 0 {
        pos_y += dir_y;
        let new_front = [];
        while front.pop_front() is Some(pos_x, carry) {
          let &char = get((pos_x, pos_y));
          when {
            char == '#' {
              ~success = false;
              new_front = [];
              break;
            }
            char == '[' {
              new_front.push_back((pos_x, '['));
              new_front.push_back((pos_x + 1, ']'));
              let carry = '.';
              if front.len() != 0 && front.at(0) is Some(&(p, c)) && p == pos_x + 1 {
                front.pop_front();
                carry = c;
              }
              let &char = get((pos_x + 1, pos_y));
              if ~success {
                char = carry;
              }
            }
            char == ']' {
              new_front.push_back((pos_x - 1, '['));
              new_front.push_back((pos_x, ']'));
              let &char = get((pos_x - 1, pos_y));
              if ~success {
                char = '.';
              }
            }
          }

          if ~success {
            char = carry;
          }
        }
        front = new_front;
      }
    }
  }

  let width = (*grid.at(0).unwrap()).len();
  let height = grid.len();

  let part2 = 0;
  let y = 0;
  for row in grid.as[List].into_iter() {
    let x = 0;
    for char in row.as[List].into_iter() {
      if char == '[' {
        part2 += y * 100 + x;
      }
      x += 1;
    }
    y += 1;
  }

  show_grid();

  io.println("Part 2: {part2}");
}
