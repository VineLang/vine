
use #root::data::Map;

const max: N32 = 75;

pub fn main(&io: &IO) {
  let input = io.read_line().assume();

  let stones = Map::empty[N64, _];
  let input = input.split(" ");
  for stone in input.into_iter() {
    let stone = N32::parse(stone).assume() as N64;
    stones.insert(stone, 1[N64]);
  }

  for i in 1..(max + 1) {
    stones = blink(&io, stones);
    io.println("{i}: {count(&stones)}");
  }
}

fn blink(&io: &IO, old: Map[N64, N64]) -> Map[N64, N64] {
  let new = Map::empty[N64, _];
  let fn add_stones(number: N64, count: N64) {
    let &c = new.at_or_insert(number, 0[N64]);
    c += count;
  }
  for (number, count) in old.into_iter() {
    if number.is_zero() {
      add_stones(1[N64], count);
    } else {
      let digits = number.log_n32(10) + 1;
      if digits % 2 == 0 {
        let d = 10 ** (digits / 2);
        let (a, b) = number.div_rem_n32(d);
        add_stones(a, count);
        add_stones(b as N64, count);
      } else {
        add_stones(number * 2024[N64], count);
      }
    }
  }
  new
}

fn count(&stones: &Map[N64, N64]) -> N64 {
  let n = 0[N64];
  for &(_, count) in stones.iter_ref() {
    n += count;
  }
  n
}
