
use #root::data::Map;

pub fn main(&io: &IO) {
  let map = Map::empty[N32, _];

  let part1 = 0[N64];

  let buyer = 0;
  while io.read_line() is Some(secret) {
    let (a, b, c, d) = (0, 0, 0, 0);
    let secret = N32::parse(secret).assume();
    let last_price = 0;
    for i in 0..black_box(2000) {
      evolve(&secret);
      let price = secret % 10;
      (a, b, c, d) = (b, c, d, price - last_price);
      last_price = price;
      if i >= 4 {
        let &(bananas, last_changed) = map.at_or_insert(
          ((a * 20 + b) * 20 + c) * 20 + d,
          (0, N32::maximum),
        );
        if last_changed != buyer {
          bananas += price;
          last_changed = buyer;
        }
      }
    }
    part1 += secret as N64;
    buyer += 1;
  }

  io.println("Part 1: {part1}");

  io.println("{map.len()}");

  let part2 = 0;
  for (_, (bananas, _)) in map.into_iter() {
    part2 = part2.max(bananas);
  }

  io.println("Part 2: {part2}");
}

fn evolve(&secret: &N32) {
  secret = (secret ^ secret << 6) & 16777215;
  secret = (secret ^ secret >> 5) & 16777215;
  secret = (secret ^ secret << 11) & 16777215;
}
