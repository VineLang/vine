
use std::{data::Array, ops::vectorized::add};

pub fn main(&io: &IO) {
  let input = io.full_input();

  let grid = input.split_trim("\n");

  let guard_pos = (0, 0);

  let y = 0;
  for &row in grid.iter() {
    let x = 0;
    for &cell in row!.iter() {
      if cell == '^' {
        guard_pos = (x, y);
        cell = 'X';
      }
      x += 1;
    }
    y += 1;
  }

  let grid = grid.map(fn* (String(chars)) { chars as Array }) as Array;

  let (_, length) = walk(&grid, guard_pos);

  io.println("Part 1: {length}");

  let spaces = [];
  clear(&grid, &spaces);

  let i = 0;
  let loops = 0;
  for (x, y) in spaces.into_iter() {
    let grid = grid;
    *(*grid.at(y).unwrap()).at(x).unwrap() = '#';
    let (looped, _) = walk(&grid, guard_pos);
    if looped {
      loops += 1;
    }
    i += 1;
    io.println("{i} {loops}");
  }

  io.println("Part 2: {loops}");
}

fn clear(&grid: &Array[Array[Char]], &spaces: &List[(N32, N32)]) {
  let height = grid.len();
  let width = (*grid.at(0).unwrap()).len();
  let y = 0;
  while y < height {
    let &row = grid.at(y).unwrap();
    let x = 0;
    while x < width {
      let &cell = row.at(x).unwrap();
      if cell == 'X' {
        spaces ++= [(x, y)];
        cell = '.';
      }
      x += 1;
    }
    y += 1;
  }
}

fn walk(&grid: &Array[Array[Char]], pos: (N32, N32)) -> (Bool, N32) {
  let height = grid.len();
  let width = (*grid.at(0).unwrap()).len();
  let dir = (0, -(1));
  let distinct = 1;
  let repeat = 0;

  let looped = while repeat < 3 * distinct {
    let new_pos = pos + dir;
    let (x, y) = new_pos;
    if x >= width || y >= height {
      break false;
    }
    let &cell = (*grid.at(y).unwrap()).at(x).unwrap();
    if cell == '#' {
      let (x, y) = dir;
      dir = (-y, x);
      continue;
    }
    if cell == 'X' {
      repeat += 1;
    } else {
      distinct += 1;
    }
    cell = 'X';
    pos = new_pos;
  } else {
    true
  };

  (looped, distinct)
}
