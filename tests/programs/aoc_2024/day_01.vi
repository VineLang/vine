
pub fn main(&io: &IO) {
  let input = io.full_input();

  let lines = input.split_trim("\n").map(fn* (line: String) {
    let parts = line.split("   ");
    let left = N32::parse(parts.pop_front().assume()).assume();
    let right = N32::parse(parts.pop_front().assume()).assume();
    (left, right)
  });

  let left = lines.map(fn* ((left, _)) { left });
  let right = lines.map(fn* ((_, right)) { right });

  left.sort();
  right.sort();

  let total_dist = 0;
  let iter_a = left.iter();
  let iter_b = right.iter();
  for a in iter_a {
    let (b, _iter_b) = iter_b.advance().assume();
    iter_b = _iter_b;
    total_dist += if a > b {
      a - b
    } else {
      b - a
    };
  }

  io.println("Total Distance: {total_dist}");

  let similarity = 0;
  for a in left.iter() {
    let count = 0;
    for b in right.iter() {
      if a == b {
        count += 1;
      }
    }
    similarity += a * count;
  }

  io.println("Similarity Score: {similarity}");
}
