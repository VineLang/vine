
use #root::{data::Array, ops::elementwise as _};

const config: (N32, N32) = (7, 12);
// const config: (N32, N32) = (71, 1024);

pub fn main(&io: &IO) {
  let (size, iters) = config;

  let grid = Array::new(size, Array::new(size, N32::maximum));
  let data = [];

  let i = 0;
  while io.read_line() is Some(line) {
    let (x, y) = line.split_once(",");
    let x = N32::parse(x).assume();
    let y = N32::parse(y.assume()).assume();
    *(*grid.at(y).assume()).at(x).assume() = i;
    data ++= [(x, y)];
    i += 1;
  }

  let bytes = i;

  let fn show_grid(c: N32) {
    io.println(grid.as[List].map(fn* (row: Array[N32]) {
      row.fold_front(
        "",
        fn* (s: String, n: N32) {
          s ++ if n < c {
            "#"
          } else {
            "."
          }
        },
      )
    }).join("\n"))
  }

  // show_grid(iters);

  show_grid(N32::maximum);

  let part1 = pathfind(grid, size, iters);

  io.println("Part 1: {part1}");

  let set = DisjointSet::new(size * size);

  let fn coord(x: N32, y: N32) {
    y * size + x
  }

  let blocked = Array::new(size, Array::new(size, true));

  let fn remove_data(x: N32, y: N32) {
    let fn connect(dx: N32, dy: N32) {
      let X = x + dx;
      let Y = y + dy;
      if X < size and Y < size and !*(*blocked.at(Y).assume()).at(X).assume() {
        set.union(coord(x, y), coord(X, Y));
      }
    }
    connect(1, 0);
    connect(-1 as N32, 0);
    connect(0, 1);
    connect(0, -1 as N32);
    *(*blocked.at(y).assume()).at(x).assume() = false;
  }

  let i = 0;
  for y in 0..size {
    for x in 0..size {
      if (*grid.at(y).assume()).get(x).assume() == N32::maximum {
        i += 1;
        remove_data(x, y);
      }
    }
  }

  let data_rev = data.reversed();

  let i = bytes;
  while set.find(0) != set.find(size * size - 1) and data_rev.pop_front() is Some(x, y) {
    remove_data(x, y);
    i -= 1;
  }

  let &(x, y) = data.at(i).assume();

  io.println("Part 2: {x},{y}");
}

struct? DisjointSet(Array[Node]);

enum* Node {
  Root(N32),
  Child(N32),
}

mod DisjointSet {
  pub fn new(len: N32) -> DisjointSet {
    DisjointSet(Array::new(len, Node::Root(1)))
  }

  pub fn .find(&DisjointSet(array), i: N32) -> N32 {
    let loop root;
    while array.at(i) is Some(&Node::Child(parent)) {
      i = parent;
      parent = root;
    }
    root = i;
    i
  }

  pub fn .union(&self: &DisjointSet, a: N32, b: N32) -> N32 {
    let a = self.find(a);
    let b = self.find(b);
    if a == b {
      return a;
    }
    let &DisjointSet(array) = &self;
    let &a_node = array.at(a).assume();
    let &b_node = array.at(b).assume();
    match (&a_node, &b_node) {
      (&Node::Root(a_size), &Node::Root(b_size)) {
        if a_size < b_size {
          a_node = Node::Child(b);
          b_size += a_size;
          b
        } else {
          b_node = Node::Child(a);
          a_size += b_size;
          a
        }
      }
      _ { debug::error("unreachable") }
    }
  }
}

fn pathfind(grid: Array[Array[N32]], size: N32, iters: N32) -> N32 {
  let cur = [(0, 0)];

  let result = 0;

  let i = 0;
  while cur.len() != 0 {
    let next = [];
    for pos in cur.iter() {
      let (x, y) = pos;
      if x == size - 1 and y == size - 1 {
        result = i;
        next = [];
        break;
      }
      let &cell = (*grid.at(y).assume()).at(x).assume();
      if cell >= iters {
        cell = 0;
        let fn visit((x: N32, y: N32)) {
          if x < size and y < size and (*grid.at(y).assume()).get(x).assume() >= iters {
            next ++= [(x, y)];
          }
        }
        visit(pos + (1, 0));
        visit(pos + (0, 1));
        visit(pos - (1, 0));
        visit(pos - (0, 1));
      }
    }
    cur = next;
    i += 1;
  }

  result
}
