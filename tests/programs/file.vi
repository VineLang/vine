
use #root::IO::File;

pub fn main(&io: &IO) {
  let write_file = File::create(&io, "/tmp/vine-file-test");
  io.println("Creating a file should succeed: {write_file.show()}");

  assert write_file is Ok(write_file) else {
    return;
  }

  write_file.write_bytes(&io, "Tripterygium wilfordii, or "!.map(fn* (c: Char) { c as N32 }));
  write_file.write_string(&io, "雷公藤.");
  write_file.close(&io);

  let read_file = File::open(&io, "/tmp/vine-file-test").assume();
  let contents = read_file.read_string(&io).assume();
  io.println("read_string after writing: {contents}");
  read_file.close(&io);

  let read_file = File::open(&io, "/tmp/vine-file-test").assume();
  let bytes = read_file.read_bytes(&io).assume();
  let bytes = bytes.map(fn* (byte: N32) { byte as Char }) as String;
  io.println("read_bytes after writing: {bytes}");
  read_file.close(&io);

  let missing_file = File::open(&io, "/tmp/no-way-this-file-exists");
  io.println("A missing file should error: {missing_file.show()}");
  match missing_file {
    Ok(f) { f.close(&io) }
    Err(_) {}
  }
}
