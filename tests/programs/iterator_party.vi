
use #root::data::Iterator;

pub fn main(_: &IO) {
  debug::log("--- concat, empty, one, from_next ---");
  let list = [1, 2, 3];
  let iter_ref = Iterator::one(0) ++ Iterator::empty ++ Iterator::from_next(fn* () {
    list.pop_front()
  });
  for x in iter_ref {
    debug::log("{x}");
  }

  debug::log("--- map, filter, flat_map, enumerate, for_each ---");
  let iter_ref = 0..10;
  let iter_ref = iter_ref.map(fn* (x: N32) { x * x });
  let iter_ref = iter_ref.filter(fn* (&x: &N32) { x % 2 != 0 });
  let iter_ref = iter_ref.flat_map(fn* (x: N32) { List::new(x, x).iter() });
  let acc = 0;
  let iter_ref = iter_ref.map(fn* (x: N32) {
    acc += x;
    acc
  });
  let iter_ref = iter_ref.enumerate();
  iter_ref.for_each(fn* ((i: N32, x: N32)) {
    debug::log("{i}: {x}");
  });

  debug::log("--- take, inspect ---");
  let iter_ref = (0..100).inspect(fn* (&x: &N32) { debug::log("{x}") });
  for x in iter_ref.take(10) {
    debug::log("{x}");
  }
  debug::log("--- take_while ---");
  for x in iter_ref.take_while(fn* (&x: &N32) { x < 10 }) {
    debug::log("{x}");
  }

  debug::log("--- skip, skip_while ---");
  let iter_ref = (0..20).skip(10).skip_while(fn* (&x: &N32) {
    debug::log("{x}");
    x < 15
  });
  for x in iter_ref {
    debug::log("{x}");
  }

  debug::log("--- zip, inspect ---");
  for (a, b) in [(5, 3), (3, 5)].iter() {
    debug::log("{a}, {b}");
    let a = (0..a).inspect(fn* (&x: &N32) { debug::log("x: {x}") });
    let b = (0..b).inspect(fn* (&y: &N32) { debug::log("y: {y}") });
    for t in a.zip(b) {
      debug::log("{t.show()}");
    }
  }

  debug::log("--- filter, all, count, any, reduce ---");
  debug::log("{
    (2..46).filter(fn* (&n: &N32) {
      debug::log("{n}?");
      (2..n).all(fn* (m: N32) {
        debug::log("{m}");
        n % m != 0
      })
    }).count()
  }");
  debug::log("{
    (2..46).filter(fn* (&n: &N32) {
      debug::log("{n}?");
      !(2..n).any(fn* (m: N32) {
        debug::log("{m}");
        n % m == 0
      })
    }).reduce(fn* (a: N32, b: N32) { a + b }).show()
  }");

  debug::log("--- inspect, nth, last, collect ---");
  let iter_ref = (0..10).inspect(fn* (&n: &N32) { debug::log("{n}") });
  debug::log("{iter_ref.nth(3).show()}");
  debug::log("{iter_ref.last().show()}");
  debug::log("{iter_ref.collect[List, _, _]().show()}");

  debug::log("--- fold ---");
  debug::log("{(0..5).fold("_", fn* (a: String, b: N32) { "({a}, {b})" })}");

  debug::log("--- filter_map, map_while, forked, collect, find, find_map ---");
  let nums = [
    [0, 1, 2, 3, 4, 5, 6, 7, 8, 9],
    [1, 3, 5, 7, 9],
    [0, 1, 1, 2, 3, 5, 8],
    [2, 3, 5, 7],
    [0, 1, 4, 9],
    [1, 2, 4, 8],
    [6],
  ];
  debug::log("{
    (0..11).map(fn* (i: N32) {
      nums.iter_ref().filter_map(fn* (&l: &List[N32]) { l.at(i) }).forked().collect[List, _, _]()
    }).collect[List, _, _]().show()
  }");
  debug::log("{
    (0..11).map(fn* (i: N32) {
      nums.iter_ref().map_while(fn* (&l: &List[N32]) { l.at(i) }).forked().collect[List, _, _]()
    }).collect[List, _, _]().show()
  }");
  nums.reverse();
  debug::log("{
    (0..11).map(fn* (i: N32) {
      nums.iter_ref().find(fn* (&&l: &&List[N32]) { l.len() > i }).as_fork()
    }).collect[List, _, _]().show()
  }")
  debug::log("{
    (0..11).map(fn* (i: N32) {
      nums.iter_ref().find_map(fn* (&l: &List[N32]) { l.at(i) }).as_fork()
    }).collect[List, _, _]().show()
  }")
}
